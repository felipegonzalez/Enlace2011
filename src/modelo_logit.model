model {
    for (i in 1:no.escuelas){
		no.bueno.esp[i] ~ dbinom(p.esp.bound[i], no.eval[i])
		no.bueno.mat[i] ~ dbinom(p.mat.bound[i], no.eval[i])
		p.esp.bound[i] <- max(0, min(1, p.esp[i]))
		p.mat.bound[i] <- max(0, min(1, p.mat[i]))
		logit(p.esp[i]) <- Xbeta.esp[i]
		logit(p.mat[i]) <- Xbeta.mat[i]
        Xbeta.esp[i] ~ dnorm(Xbeta.0[i] + esp.aj, tau.esp)
		Xbeta.mat[i] ~ dnorm(Xbeta.0[i], tau.mat)
		Xbeta.0[i] <- a.0 + a.tipo[tipo[i]] + a.marg[marg[i]] + a.estado[estado[i]]
	}
	
	esp.aj ~ dnorm(0, 0.001)
	a.adj <- a.0 + mean(a.tipo[]) + mean(a.marg[])
	a.0 ~ dnorm(0, 0.0001)
	sigma.mat ~ dunif(0.001, 1000)
	sigma.esp ~ dunif(0.001, 1000)
	tau.mat <- pow(sigma.mat, -2)
	tau.esp <- pow(sigma.esp, -2)

	for(t in 1:n.tipo){
		a.tipo[t] ~ dnorm(0, tau.tipo)
		a.tipo.adj[t] <- a.tipo[t] - mean(a.tipo[])
	}
	tau.tipo <- pow(sigma.tipo, -2)
	sigma.tipo ~ dunif(0.001, 1000)
	
	for(m in 1:n.marg){
		a.marg[m] ~ dnorm(0, tau.marg)
		a.marg.adj[m] <- a.marg[m] - mean(a.marg[])
	}
	tau.marg <- pow(sigma.marg, -2)
	sigma.marg ~ dunif(0.001, 1000)	
	for(e in 1:n.estados){
		a.estado[e] ~ dnorm(0, tau.estado)
		a.estado.adj[e] <- a.estado[e] - mean(a.estado[])
	}
	tau.estado <- pow(sigma.estado, -2)
	sigma.estado ~ dunif(0.001, 1000)	

}